allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    ext {
        husbylibVersion = "0.1." + ('git rev-list --count HEAD'.execute().text.trim())
        gsonVersion = "2.8.0"
        guavaVersion = "30.1.1-jre"
        mysqlVersion = "8.0.26"
        hikariVersion = "4.0.3"
        lombokVersion = "1.18.20"
    }

    group "com.noahhusby.lib"
    version "$husbylibVersion"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        if (!("common".equalsIgnoreCase(project.name))) {
            implementation project(":common")
        }

        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    publishing {
        repositories {
            if (System.getenv("MAVEN_USERNAME") != null && System.getenv("MAVEN_PASSWORD")) {
                maven {
                    credentials {
                        username = "$MAVEN_USERNAME"
                        password = "$MAVEN_PASSWORD"
                    }
                    name = "HusbyLib"
                    url = "https://maven.noahhusby.com/releases"
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId = "com.noahhusby.lib"
                artifactId = "HusbyLib".equalsIgnoreCase(project.name) ? "all" : project.name
                version = project.version

                from components.java
            }
        }
    }
}

dependencies {
    for (Project prj : project.allprojects) {
        if (!"HusbyLib".equalsIgnoreCase(prj.name) && prj.parent.name.equalsIgnoreCase("HusbyLib")) {
            implementation project(":" + prj.name)
        }
    }
}